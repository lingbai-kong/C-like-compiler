<PROGRAM>::=<M><ASSERTIONS>
<M>::='epsilon'
<ASSERTIONS>::=<ASSERTION>|<ASSERTION><ASSERTIONS>
<ASSERTION>::='INT''ID'<ASSERTIONTYPE>'DEL'|<FUNCASSERTION><SENBLOCK>
<ASSERTIONTYPE>::='epsilon'|<ARRAYASSERTION>
<FUNCASSERTION>::='VOID''ID'<M>'LP'<FORMALPARAM>'RP'|'INT''ID'<M>'LP'<FORMALPARAM>'RP'
<ARRAYASSERTION>::='LS''NUM''RS'|'LS''NUM''RS'<ARRAYASSERTION>
<FORMALPARAM>::=<FORMALPARAMLIST>|'VOID'|'epsilon'
<FORMALPARAMLIST>::='INT''ID'|'INT''ID''SEP'<FORMALPARAMLIST>
<SENBLOCK>::='LB'<INNERASSERTION><SENSEQ>'RB'
<INNERASSERTION>::=<INNERVARIDEF>'DEL'<INNERASSERTION>|'epsilon'
<INNERVARIDEF>::='INT''ID'|'INT''ID'<ARRAYASSERTION>
<SENSEQ>::=<SENTENCE>|<SENTENCE><SENSEQ>
<SENTENCE>::=<IFSEN>|<WHILESEN>|<RETURNSEN>'DEL'|<ASSIGNMENT>'DEL'
<ASSIGNMENT>::='ID''ASSIGN'<EXPRESSION>|<ARRAY>'ASSIGN'<EXPRESSION>
<RETURNSEN>::='RETURN'<EXPRESSION>|'RETURN'
<WHILESEN>::=<B>'WHILE''LP'<CTRL>'RP'<T><SENBLOCK>
<B>::='epsilon'
<IFSEN>::='IF''LP'<CTRL>'RP'<T><SENBLOCK>|'IF''LP'<CTRL>'RP'<T><SENBLOCK>'ELSE'<N><SENBLOCK>
<CTRL>::=<EXPRESSION>
<T>::='epsilon'
<N>::='epsilon'
<EXPRESSION>::=<BOOLAND>|<BOOLAND>'OR'<EXPRESSION>
<BOOLAND>::=<BOOLNOT>|<BOOLNOT>'AND'<BOOLAND>
<BOOLNOT>::=<COMP>|'NOT'<COMP>
<COMP>::=<PLUSEX>|<PLUSEX>'RELOP'<COMP>
<PLUSEX>::=<TERM>|<TERM>'OP1'<PLUSEX>
<TERM>::=<FACTOR>|<FACTOR>'OP2'<TERM>
<FACTOR>::='NUM'|'LP'<EXPRESSION>'RP'|'ID'|<ARRAY>|'ID'<CALL>|'LP'<ASSIGNMENT>'RP'
<CALL>::='LP'<ACTUALPARAM>'RP'
<ARRAY>::='ID''LS'<EXPRESSION>'RS'|<ARRAY>'LS'<EXPRESSION>'RS'
<ACTUALPARAM>::=<ACTUALPARAMLIST>|'epsilon'
<ACTUALPARAMLIST>::=<EXPRESSION>|<EXPRESSION>'SEP'<ACTUALPARAMLIST>